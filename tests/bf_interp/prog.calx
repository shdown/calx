fun Interpret(code) {
    NMEM := 30000
    mem := []
    for (i := 0; i < NMEM; i += 1) {
        mem[i] = 0
    }
    memi := 0

    stack := []
    for (i := 0; i < @code; i += 1) {
        c := code[i]

        if (c == "+") {
            mem[memi] += 1
            mem[memi] &= 255

        } elif (c == "-") {
            mem[memi] -= 1
            mem[memi] &= 255

        } elif (c == "<") {
            memi -= 1

        } elif (c == ">") {
            memi += 1

        } elif (c == ".") {
            RawWrite(Chr(mem[memi]))

        } elif (c == ",") {
            byte := RawRead("B")
            if (byte == "") {
                mem[memi] = 255
            } else {
                mem[memi] = Ord(byte)
            }

        } elif (c == "[") {
            if (mem[memi] == 0) {
                i += 1
                for (b := 1; b != 0; i += 1) {
                    c := code[i]
                    if (c == "[") {
                        b += 1
                    } elif (c == "]") {
                        b -= 1
                    }
                }
                i -= 1
            } else {
                stack[@stack] = i
            }

        } elif (c == "]") {
            i = Pop(stack) - 1
        }
    }
}

Interpret("++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.")
